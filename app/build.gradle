apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

def apiKeysPropertiesFile = rootProject.file("apikeys.properties")
def apiKeysProperties = new Properties()
apiKeysProperties.load(new FileInputStream(apiKeysPropertiesFile))

// https://blog.jetbrains.com/kotlin/2017/08/kotlin-1-1-4-is-out
androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.chipcerio.newsly"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    buildTypes.each {
        it.buildConfigField('String', 'NEWS_API_KEY', apiKeysProperties['NewsApiKey'])
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // support library
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'

    // testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    // reactive extensions
    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    // network
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'

    // dependency injection
    implementation 'com.google.dagger:dagger:2.11'
    kapt 'com.google.dagger:dagger-compiler:2.11'
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    // persistence
    implementation 'android.arch.persistence.room:runtime:1.0.0'
    kapt 'android.arch.persistence.room:compiler:1.0.0'
    implementation 'android.arch.persistence.room:rxjava2:1.0.0'

    // image loading
    implementation 'com.github.bumptech.glide:glide:4.1.1'
    kapt 'com.github.bumptech.glide:compiler:4.1.1'

    // utilities
    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

}
